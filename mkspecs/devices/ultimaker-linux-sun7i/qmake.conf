#
# qmake configuration for linux-g++ (Ultimaker Olimex specific)
#

include(../common/linux_device_pre.conf)

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental optimize_full
QMAKE_INCREMENTAL_STYLE = sublib

include(../../common/linux.conf)
include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)

!load(device_config) {
  error(Could not successfully load device configuration)
}

# modifications to g++-unix.conf
QMAKE_CC                = $${CROSS_COMPILE}gcc
QMAKE_CXX               = $${CROSS_COMPILE}g++
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${CROSS_COMPILE}ar cqs
QMAKE_OBJCOPY           = $${CROSS_COMPILE}objcopy
QMAKE_NM                = $${CROSS_COMPILE}nm -P
QMAKE_STRIP             = $${CROSS_COMPILE}strip

# modifications to gcc-base.conf
QMAKE_AR_LTCG           = $${CROSS_COMPILE}gcc-ar cqs
QMAKE_NM_LTCG           = $${CROSS_COMPILE}gcc-nm -P

contains(DISTRO_OPTS, deb-multi-arch): \
QMAKE_PKG_CONFIG = $${CROSS_COMPILE}pkg-config

QMAKE_CFLAGS            += -march=armv7ve -marm -mthumb-interwork -mfpu=neon-vfpv4 -mtune=cortex-a7 -mabi=aapcs-linux -mfloat-abi=hard
QMAKE_CXXFLAGS          = $$QMAKE_CFLAGS


#include(../common/linux_arm_device_post.conf)
contains(DISTRO_OPTS, hard-float) {
  COMPILER_FLAGS += -mfloat-abi=hard -DULTIMAKER_CARD0
  LINKER_FLAGS   += -mfloat-abi=hard
} else: !contains(DISTRO_OPTS, aarch64) {
  COMPILER_FLAGS += -mfloat-abi=softfp
  LINKER_FLAGS   += -mfloat-abi=softfp
}

load(qt_config)
